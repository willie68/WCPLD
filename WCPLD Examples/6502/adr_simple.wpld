header:
Name     adr_simple ;
PartNo   01 ;
Date     24.07.2022 ;
Revision 03 ;
Designer wkla ;
Company  nn ;
Assembly None ;
Location  ;
Device   G16V8 ;

pld:
/* *************** INPUT PINS *********************/
PIN [1..8]   =  [A15..A8]; 
PIN 9   =  PHI2;

/* *************** OUTPUT PINS *********************/
PIN 12   =  CSRAM;
PIN 13   =  CSHIROM;
PIN 14   =  CSEXTROM;
PIN 15   =  IOPORT;
PIN 16   =  CSIO3PORT;
PIN 17   =  CSIO2PORT;
PIN 18   =  ACIAPORT;
PIN 19   =  VIAPORT;
/* *************** LOGIC *********************/

FIELD Addr = [A15..A8];
CSRAM_EQU = Addr:[0000..7FFF]; // 32KB

/* IO */
IOPORT    = ! (Addr:[B000..BFFF]); // 4KB
VIAPORT   = ! (Addr:[B000..B0FF]);
ACIAPORT  = ! (Addr:[B100..B1FF]);
CSIO2PORT = ! (Addr:[B200..B2FF]);
CSIO3PORT = ! (Addr:[B300..B3FF]);

/* 12KB of external ROM */
CSEXTROM = ! (Addr:[8000..AFFF]); // 12KB
/* 8kb of ROM */
CSHIROM = ! (Addr:[C000..FFFF]);  // 16KB

/* RAM */
CSRAM = !CSRAM_EQU # !PHI2;

simulator:
ORDER: A15, A14, A13, A12, A11, A10, A9, A8, PHI2, CSEXTROM, CSRAM, CSHIROM, IOPORT, VIAPORT, ACIAPORT, CSIO2PORT, CSIO3PORT; 

VECTORS:
/* internal RAM */
0 X X X X X X X 0 H H H H H H H H 
0 X X X X X X X 1 H L H H H H H H 

/* 8000-AFFF external Rom */ 
1 0 0 0 X X X X X L H H H H H H H 
1 0 0 1 X X X X X L H H H H H H H 
1 0 1 0 X X X X X L H H H H H H H 

/* IO */ 
/* CSIO0 */
1 0 1 1 0 0 0 0 X H H H L L H H H 
/* CSIO1 */
1 0 1 1 0 0 0 1 X H H H L H L H H 
/* CSIO2 */
1 0 1 1 0 0 1 0 X H H H L H H L H 
/* CSIO3 */
1 0 1 1 0 0 1 1 X H H H L H H H L 
/* nicht direkt benutzt */
1 0 1 1 0 1 X X X H H H L H H H H 
1 0 1 1 1 X X X X H H H L H H H H 
/* ROM */
1 1 X X X X X X X H H L H H H H H 
